/** MODULE: Simple Wyvern class example from http://www.cs.cmu.edu/~aldrich/securemobileweb/spec-rationale.html **/


/** Public Interface **/
type IntCell
	var contents : Int

type Stack
	prop top : Int?
	meth push(element : Int)
	meth pop() : Int?

type StackFactory
	meth Stack() : Stack
	meth StackWithFirst(firstElement : Int) : Stack

type ListUtilities
	meth map(f : Int -> Int, l : IntList) : IntList


/** Private Implementation **/
class StackImpl
	implements Stack
	class implements StackFactory

	var list : Link?

	meth top() : Int? = list.data

	meth push(element : Int)
		list = Link(element, list)

	meth pop()
		val result = list.data
		list = list.next
		result

	class meth Stack() : Stack = new StackImpl
	
	class meth StackWithFirst(firstElement : Int) : Stack
		new StackImpl
			list = Link(firstElement, null)


class Link
	val data : Int
	val next : Link?

/* The following is a question of whether Link class method and Link class when used by new should conflict or not? */
/*
	class meth Link(d:Int, n:Link?)
		new Link
			data=d
			next=n
*/


meth stackClient()
	val s = StackImpl.Stack()
	s.push(5)
	print(s.top)
	val addOne = fn x:Int => x+1
	print(addOne(s.pop()))