/** MODULE: Simple Wyvern class example from http://www.cs.cmu.edu/~aldrich/securemobileweb/spec-rationale.html **/


/** Public Interface **/
type IntCell
	var contents : Int

type Stack
	prop top : Int?
	fun push(element : Int)
	fun pop() : Int?

type StackFactory
	fun Stack() : Stack
	fun StackWithFirst(firstElement : Int) : Stack


/** Private Implementation **/
class Link
	val data : Int
	val next : Link?

	class fun Link(d:Int, n:Link?) : Link
		new
			data=d
			next=n


class StackImpl
	implements Stack
	class implements StackFactory

	var list : Link?

	fun top() : Int? = this.list.data

	fun push(element : Int) : Unit
		this.list = Link.Link(element, (this.list))

	fun pop() : Int?
		val result : Int = this.list.data
		this.list = this.list.next
		result

	class fun Stack() : Stack = new
	
	class fun StackWithFirst(firstElement : Int) : Stack
		new
			list = Link(firstElement, null)


fun stackClient()
	val s : Stack = StackImpl.Stack()
	s.push(5)
	printInteger(s.top)
	val addOne:Int->Int = fn x:Int => x+1
	printInteger(addOne(s.pop()))